/* https://github.com/mkraska/meclib/wiki/Feedback-Overview */ 
fb_value_version: "2025 03 02, 10:00";

/* https://github.com/mkraska/meclib/wiki/fb_vars */
fb_strip_list(_list_of_things):= castext("[[define _first='true'/]][[foreach _thing='_list_of_things']][[if test='not _first']], [[else]][[define _first='false'/]][[/if]]{@_thing@}[[/foreach]]"); 
/* fb_strip_list function proposed by Matti Harjula 2022 04 10 */

fb_vars(%_sans,%_tans, [%_opt] ) := block([%_vars, %_label,%_tansvar,%_sansvar,%_extras, %_missing, simp, %_txt, %_L],
    str_unexpected_variable: castext("[[lang code='de']]&nbsp;Unerwartete Variable [[/lang]][[lang code='en,other']]&nbsp;Unexpected variable [[/lang]]"),
    str_missing_variable: castext("[[lang code='de']]&nbsp;Fehlende Variable [[/lang]][[lang code='en,other']]&nbsp;Missing variable [[/lang]]"),
    str_check_term: castext("[[lang code='de']]&nbsp;Pr&uuml;fen Sie den Term mit [[/lang]][[lang code='en,other']]&nbsp;Check the term with [[/lang]]"),
    str_check_terms: castext("[[lang code='de']]&nbsp;Pr&uuml;fen Sie die Terme mit [[/lang]][[lang code='en,other']]&nbsp;Check the terms with [[/lang]]"),

    simp: true, /* Extract vars with simp:true does mean that "x/x+y" -> [y] */
    if length(%_opt)>0 then %_vars: %_opt[1] else %_vars: [], 
    if length(%_opt)>1 then %_label: %_opt[2] else %_label: "",
    %_txt: %_label,
    %_sansvar: setify(listofvars(%_sans)),
    %_tansvar: setify(listofvars(%_tans)),
    /* Say nothing if tans is free of vars and is zero) */
    if (emptyp(%_tansvar) and %_tans=0) then return(""),
    /* Check for spurious (extra) vars */
    %_extras: setdifference(%_sansvar, %_tansvar),
    if length(%_extras) > 0 then (
      %_txt: castext_concat(%_txt, str_unexpected_variable),
      %_txt: castext_concat(%_txt, fb_strip_list(listify(%_extras)) ),
      %_txt: castext_concat(%_txt, "."  ) ),
    /* Check for missing vars */
    %_missing: setdifference(%_tansvar, %_sansvar),
    if length(%_missing) > 0 then  (
      %_txt: castext_concat(%_txt, str_missing_variable ),
      %_txt: castext_concat(%_txt, fb_strip_list(listify(%_missing)) ),
      %_txt: castext_concat(%_txt,  castext(" (<code>{@stack_disp_comma_separate(listify(%_missing))@}</code>)." ))),
    if %_txt # %_label then return(%_txt),
    /* if set of vars is ok, then check for the terms with vars from given list (%_vars). */ 
    %_L: sublist (%_vars, lambda ([x], diff(%_sans-%_tans, x)#0)),
    if length(%_L)=0 then return(""),
    if length(%_L)=1 then (
      %_txt: castext_concat( %_txt, str_check_term ),
      return( castext_concat (%_txt, castext("{@%_L[1]@}.") ) )
    ),
    %_txt: castext_concat( %_txt, str_check_terms ), 
    %_txt: castext_concat( %_txt, fb_strip_list(%_L) ), 
    castext_concat(%_txt, "." ) 
);	
/* this should return true if the check passes, false otherwise */
fb_varsOK(%_sans,%_tans, [%_opt] ):= block([%_res],
  if length(%_opt)=0 then %_res: fb_vars(%_sans,%_tans ),
  if length(%_opt)=1 then %_res: fb_vars(%_sans,%_tans, %_opt[1] ),
  if length(%_opt)=2 then %_res: fb_vars(%_sans,%_tans, %_opt[1], %_opt[2] ),
  stringp(%_res));


/* https://github.com/mkraska/meclib/wiki/fb_unit */
fb_unit(%_sans, %_tans, %_tol) := block([%_snum, %_tnum, %_sunit, %_tunit, %_oom, %_mnt, %_d, %_d0, simp, debug1],
  simp:true,
  /* Debug strings */
  if debug then debug1: "tnum=0 and snum=0 (because debug=true. Would be empty otherwise.)" else debug1:"",
  /* extract numbers and units */
  %_snum: float(simplify(stack_units_nums(stack_unit_si_to_si_base(%_sans)))),
  if %_snum = NULLNUM then %_snum:1.0, 
  %_tnum: float(simplify(stack_units_nums(stack_unit_si_to_si_base(%_tans)))),
  if %_tnum = NULLNUM then %_tnum:1.0, 
  %_sunit: stack_units_units(stack_unit_si_to_si_base(%_sans)),
  if %_sunit = NULLUNITS then %_sunit:0,  /* display as 0 if not correct */
  %_tunit: stack_units_units(stack_unit_si_to_si_base(%_tans)),
  if %_tunit = NULLUNITS then %_tunit:0,  /* display as 0 if not correct */
  /* complain if units don't match */
  if (%_sunit # %_tunit) and (%_sunit # 0) then return(   
    castext_concat("&nbsp;Unerwartete Basiseinheit: ", castext("{@(stack_disp_fractions(\"i\"),%_sunit)@}"  )  )),   
  if (%_sunit # %_tunit) and (%_sunit = 0) and (%_snum # 0.0) then return(   
   "&nbsp;Der Wert sollte nicht dimensionslos sein."),   
  /* if tol=0 just indicate deviation */
  if (%_tol=0 or %_tol=0.0) and %_tnum # 0.0 then return(sconcat("&nbsp;Abweichung: ", ev( round((%_snum-%_tnum)/abs(%_tnum)*1000.0)/10.0, simp) , "%")), 
  /* special feedback if teacher's answer is zero */
  if %_tnum = 0.0  and %_snum  # 0.0 then return("&nbsp;Der Wert ist betragsm&auml;&szlig;ig zu gro&szlig;."),
  if %_tnum = 0.0  then return(debug1), 
  if %_snum = 0.0 then return("&nbsp;Der Wert ist betragsm&auml;&szlig;ig zu klein."),
  /* check of the sign */
  if ev(%_snum/%_tnum, simp) <0 then return("&nbsp;Pr&uuml;fen Sie das Vorzeichen."),
  /* go back if error is less than tolerance */ 
  if ev(abs((%_snum-%_tnum)/%_tnum),simp) < %_tol then (
    if debug then return(sconcat("&nbsp;Abweichung: ", ev( round((%_snum-%_tnum)/abs(%_tnum)*1000.0)/10.0, simp) , "% (display because debug=true. Would be empty otherwise.)")) 
    else return("") 
  ),
  /* OoM and mantissa error */
  %_oom: ev(log(abs(%_snum/%_tnum))/log(10.0), simp),
  %_mnt: ev(10.0^( %_oom - round(%_oom)),simp),
  /* Mantissa check */
  if ev(abs(1.0-%_mnt), simp) <%_tol and ev(abs(%_oom), simp)>0.1 then 
    return("&nbsp;Die Mantisse stimmt, die Zehnerpotenz weicht aber um mindestens eine Gr&ouml;&szlig;enordnung ab. Pr&uuml;fen Sie Ihre Einheitenumrechnung. ") 
  /* general hint on precision */
  else(
    %_d0: ev(abs((1.0-abs(%_snum)/abs(%_tnum)))*100.0, simp),  /* % Abweichung */
    %_oom: ev(10.0^round(log(%_d0)/log(10.)-0.5), simp),
    %_d: ev(round(floor(%_d0/%_oom))*%_oom, simp),
    if %_d>=1 then %_d: ev(round(%_d),simp),
    if ev(abs(%_snum)<abs(%_tnum), simp) then        
        return(sconcat("&nbsp;Der Betrag ist um mindestens ", %_d , "% zu klein.")) else
        return(sconcat("&nbsp;Der Betrag ist um mindestens ", %_d , "% zu gro&szlig;."))
 ), 
[%_snum,%_tnum, %_oom, %_d, %_mnt, %_d0] );

/* this should return true if the check passes, false otherwise */
fb_unitOK(%_sans, %_tans, %_tol):=stringp(fb_unit(%_sans, %_tans, %_tol));

/* https://github.com/mkraska/meclib/wiki/fb_number */  
fb_number(%_sans, %_tans, %_tol) := fb_unit(%_sans, %_tans, %_tol);
/* this should return true if the check passes, false otherwise */
fb_numberOK(%_snum, %_tnum, %_tol):=stringp(fb_number(%_snum, %_tnum, %_tol));

/* https://github.com/mkraska/meclib/wiki/fb_dim */
fb_dim(%_res, %_vars, %_dims):=rat(subst(map("=", %_vars, map("*", %_vars, %_dims)),%_res)/%_res);

